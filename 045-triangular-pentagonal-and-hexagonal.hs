#!/usr/bin/env runhaskell 
{- https://projecteuler.net/problem=45
Problem 45
Triangular, pentagonal, and hexagonal

Triangle, pentagonal, and hexagonal numbers are generated by the following formulae:

Triangle	 	T_{n}=n(n+1)/2	 	1, 3, 6, 10, 15, ...
Pentagonal	 	P_{n}=n(3n−1)/2	 	1, 5, 12, 22, 35, ...
Hexagonal	 	H_{n}=n(2n−1)	 	1, 6, 15, 28, 45, ...

It can be verified that T_{285} = P_{165} = H_{143} = 40755.

Find the next triangle number that is also pentagonal and hexagonal.
-}

triangles :: [Int]
triangles = map (\n -> n * (n + 1) `quot` 2) [1..]

pentagonals :: [Int]
pentagonals = map (\n -> n * (3 * n - 1) `quot` 2) [1..]

hexagonals :: [Int]
hexagonals = map (\n -> n * (2 * n - 1)) [1..]

eqs :: [Int] -> [Int] -> [Int]
eqs [] _ = []
eqs _ [] = []
eqs xss@(x:xs) yss@(y:ys)
  | x == y    = x : eqs xs ys
  | x < y     = eqs xs yss
  | otherwise = eqs xss ys

main :: IO ()
main = print . head . filter (> 40755) $ tph
   where tph = eqs tp hexagonals 
         tp = eqs triangles pentagonals   
